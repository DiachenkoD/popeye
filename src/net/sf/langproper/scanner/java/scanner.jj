/**
 Popeye - Java (Language) Properties File Editor

 Copyright (C) 2005 Raik Nagel <kiar@users.sourceforge.net>
 All rights reserved.

 Redistribution and use in source and binary forms, with or without
 modification, are permitted provided that the following conditions are met:

 * Redistributions of source code must retain the above copyright notice,
  this list of conditions and the following disclaimer.
 * Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.
 * Neither the name of the author nor the names of its contributors may be
  used to endorse or promote products derived from this software without
  specific prior written permission.

 THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
 PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER
 OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
 EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
 PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
 OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
 WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
 OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
 ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

/*
// created by : r.nagel 15.09.2005
//
// function : the java parser defintion
//
// todo     :
//
// modified :
*/

options
{
  STATIC = true ;
  JAVA_UNICODE_ESCAPE = true ;
}

PARSER_BEGIN(CodeScanner)
  /**
  * it should be a fault-tolerant java parser, hopefully
  */

  package net.sf.langproper.scanner.java ;

  import java.io.Reader ;
  import net.sf.langproper.scanner.* ;

  public class CodeScanner
  {
    private static Token memToken ;

    private static TKeyRefList keyList ;
    private static String currentFileName ;  // name of the current file

    public static void main( String args[]) throws ParseException, TokenMgrError
    {
      // no arguments -> read from std-in
      parseFile( args[0] ) ;
    }

    private static void parseFile( String filename ) throws ParseException, TokenMgrError
    {
      System.out.println("java source scanner :  Reading from file " + filename + " . . .");
      try
      {
        CodeScanner parser = new CodeScanner (new java.io.FileInputStream(filename));
        Token t = parser.JavaCompilationUnit() ;

        System.out.println("ready") ;
        System.out.println("Ausgabe : ") ;
        parser.writeOut2(t);

      } catch (java.io.FileNotFoundException e)
      {
          System.out.println("fault-tolerant Java Source Scanner (version 1.0):  File " + filename + " not found.");
          return;
      }
    }

    // ------------------------------------------------------------------------

    public static void setKeyList( TKeyRefList kList)
    {
      keyList = kList ;
    }

    public static TKeyRefList getKeyList()
    {
       return keyList ;
    }

    public static void setCurrentFileName( String filename)
    {
      currentFileName = filename ;
    }

    public static String getCurrentFileName()
    {
      return currentFileName ;
    }

    // ------------------------------------------------------------------------

    // output methode
    private void writeOut2(Token tokenList)
    {
       boolean LeerZeichen = false ;
       boolean Schleife = true ;

       Token t = tokenList ;

       while (t != null)
       {
          System.out.println(t +"#" +t.kind +"#") ;
          t = t.next ;
        }
     }

  }

PARSER_END(CodeScanner)

/* WHITE SPACE */

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

/* COMMENTS */

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}


/* LITERALS */



TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  <#HEX_NUMB: ["0"-"9","a"-"f","A"-"F"]>
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
        | ( "\\u" <HEX_NUMB> <HEX_NUMB> <HEX_NUMB> <HEX_NUMB> )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
        | ( "\\u" <HEX_NUMB> <HEX_NUMB> <HEX_NUMB> <HEX_NUMB> )
      )*
      "\""
  >

}

/* SEPARATORS */

TOKEN :
{
  < DOT: "." >
| < LPAREN: "(" >
| < RPAREN: ")" >
| < BRAKET: "{" >
| < APLUS:  ("+" | "=" | "return" | "?" | ":" | "!")+ >


}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: (<LETTER>|<DOT>) (<DOT>|<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}


/* OPERATORS */

TOKEN :
{
  <OTHER:
    ">" | "<" | "~" | "=="
   | "<=" | ">=" | "||" | "&&" | "++" | "--"
   | "-" | "*" | "/" | "&" | "|" | "^" | "%"
   | "-=" | "*=" | "/="
   | "&="

   | "}" | "[" | "]" | ";" >
/*
| < RUNSIGNEDSHIFT: ">>>" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LSHIFTASSIGN: "<<=" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
*/
}


/*****************************************
 * THE JAVA LANGUAGE GRAMMAR STARTS HERE *
 *****************************************/

/*
 * Program structuring syntax follows.
 */


Token JavaCompilationUnit() :
/*
 * Use this to parse a Java compilation unit.
 */
{
        Token t = getToken(1);
}
{
  ( Sequence() )*
  <EOF>

          { return t;}
}


void Sequence() :
{}
{
   <IDENTIFIER> | Zahl() | Other() | List() |
     Assign() | Bracket() | LParen() | RParen()
}


void LParen():
{ Token t = getToken(0) ; }
{
  <LPAREN> (Zeichen() | Sequence() )
  { memToken =  t ; }
}

void RParen():
{ memToken = getToken(0) ; }
{
  <RPAREN> [<DOT>] (Zeichen() | Sequence() )
}


void Bracket():
{ memToken = getToken(0) ; }
{
  <BRAKET> ( Zeichen() | Sequence() )
}

void List():
{ memToken = getToken(0) ; }
{
  "," (Sequence() | Zeichen() )
}


void Assign() :
{ memToken = getToken(0) ; }
{
  <APLUS> ( Zeichen() | Sequence() )
}

void Other():
{ memToken = getToken(0) ; }
{
  (<OTHER> [<DOT>] [Zeichen()])   | <CHARACTER_LITERAL>
}

void Zahl():
{}
{
  <INTEGER_LITERAL> | <FLOATING_POINT_LITERAL>
}


void Zeichen():
{ Token t = getToken(1) ; }
{
  <STRING_LITERAL> [ <DOT> ]

  {
//     System.out.println(t +" from " +memToken) ;
    String str = t.image ;
    if (str != null)
    {
      int len = str.length() ;
      if (len > 2)  // no empty strings like ""
      {
        str = str.substring(1, len -1) ;
        keyList.addKey( str, currentFileName, t.beginLine, t.beginColumn) ;
      }
    }
  }

}



